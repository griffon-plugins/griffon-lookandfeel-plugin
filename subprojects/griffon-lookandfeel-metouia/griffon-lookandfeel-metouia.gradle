apply plugin: 'groovy'
apply plugin: 'org.codehaus.griffon.griffon'
apply plugin: 'com.github.johnrengelman.shadow'

apply from: rootProject.file('gradle/code-coverage.gradle')

repositories {
    flatDir { dir 'lib' }
}

configurations {
    uberjar
}

dependencies {
    compileOnly "org.codehaus.griffon:griffon-core-compile:${griffonVersion}"

    compile project(':griffon-lookandfeel-core')
    uberjar ':metouia:1.0.0-beta'

    testCompileOnly "org.codehaus.griffon:griffon-groovy-compile:${griffonVersion}"

    testCompile project(':griffon-lookandfeel-core-test')
    testCompile "org.codehaus.griffon:griffon-groovy:${griffonVersion}"
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testCompile 'cglib:cglib-nodep:3.1'

    testRuntime "org.codehaus.griffon:griffon-guice:${griffonVersion}"
    testRuntime "org.codehaus.griffon:griffon-swing:${griffonVersion}"
    testRuntime 'org.slf4j:slf4j-simple:1.7.12'
}

sourceSets {
    main {
        compileClasspath += [configurations.uberjar]
    }
    test {
        compileClasspath += [configurations.uberjar]
        runtimeClasspath += [configurations.uberjar]
    }
}

javadoc {
    classpath += [configurations.uberjar]
}

idea {
    module {
        scopes.PROVIDED.plus += [configurations.uberjar]
    }
}

compileGroovy.enabled = false

shadowJar {
    configurations = [project.configurations.uberjar]
    baseName = project.name
    classifier = ''

    manifest {
        attributes(
            'Built-By': System.properties['user.name'],
            'Created-By': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString(),
            'Build-Date': buildDate,
            'Build-Time': buildTime,
            'Specification-Title': project.name,
            'Specification-Version': project.version,
            'Specification-Vendor': 'griffon-framework.org',
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor': 'griffon-framework.org'
        )
    }
}
jar.finalizedBy shadowJar